
// THE HUNGRY
/*
[7:19 PM] Marrub: l; SFJASDFJ:SDLFJ:LSDJF;j
[7:19 PM] Marrub: GOMH
[7:19 PM] Marrub: FLECJFLJE|
[7:19 PM] Marrub: j[]
[7:19 PM] Yholl: YEEEEEE?
[7:19 PM] Marrub: jEHPF
[7:19 PM] Marrub: kA"WDPAJZQW
[7:19 PM] Marrub: HEPLp
[7:19 PM] Marrub: WTF
[7:19 PM] Yholl: YEEE
[7:20 PM] Marrub: HELP
[7:20 PM] Yholl: wud u do
[7:20 PM] Marrub: WHY ARE THERE HUNGRY WHEN I HAVE NOMONSTERS ON
*/

/*
[4:15 AM] Yholl: I remember traumatizing mab with nomonsters again
[4:15 AM] Marrub: I was so confused during testing
[4:15 AM] Marrub: I thought there was some weird weapon or something
[4:15 AM] Marrub: and went to check it out
[4:16 AM] Marrub: THAT WAS NOT A WEAPON
[4:16 AM] Marrub: THAT WAS TEETH
[4:16 AM] Yholl: UNLIMITED TEETH WORKS
[4:16 AM] Marrub: I seriously walked in there and fucking screamed at like 2 in the morning
[4:16 AM] Yholl: hahahahahahaha
[4:16 AM] Marrub: absolute 100% terror
*/
ACTOR RLArmageddonLostSoul : RLLostSoulBase 16559
{
  Health 10
  GibHealth -40
  Radius 14
  Height 30
  Speed 4
  Mass 80
  Scale 0.7
  RenderStyle Normal
  BloodColor "Blue"
  
  Painchance 224
  MinMissileChance 0
  
  DamageFactor "Maser", 2.0 // Final Doomer Damagetype
  
  Damage 1
  DamageType "Melee"
  PainType "Melee"
  DeathType "Melee"
  
  SeeSound "thehungry/sight"
  ActiveSound "thehungry/active"
  AttackSound "thehungry/attack"
  PainSound "thehungry/pain"
  DeathSound "thehungry/death"
  
  +QUICKTORETALIATE
  +THRUSPECIES
  +MISSILEMORE
  +MISSILEEVENMORE
  +THRUGHOST
  +NOFEAR
  +SEEINVISIBLE
  +SPAWNFLOAT
  +BLOODSPLATTER
  -NOBLOOD
  
  Species "RLTheHungry"
  Obituary "%o was consumed by The Hungry."
  Tag "The Hungry"
    //$category "MP Armageddon"
  
	// Guncaster compatibility
	DropItem "Dosh1", 128
	DropItem "Dosh1", 128
	DropItem "Dosh1", 192
  States
  {
  Spawn:
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    TNT1 A 0 A_SetAngle(random(0,359))
    TNT1 A 0 A_Jump(200, 2)
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
  SpawnLoop:
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    ASOL C 6 A_LookEx (LOF_NOSIGHTCHECK, 0, 0, 0, 0, "Wakeup")
    Goto SpawnLoop
  Wakeup:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    ASOL D 6
    Goto See
	
  TheHuntIsOn:
    TNT1 A 0 A_AlertMonsters
  See:
    TNT1 A 0 A_TakeInventory("RLTheHungryGetHungrier", 1)
    ASOL AAAABBBB random(1,3) A_Chase
    Loop
  SpawnHunt:
    TNT1 A 0 A_TakeInventory("RLTheHungryGetHungrier", 99999)
    TNT1 A 0 A_SetAngle(random(0,359))
    TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * 6.4, 0, Sin(Pitch) * -6.4, 1)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    TNT1 A 0 A_Stop
    ASOL AABBAABB 2 A_LookEx (0, 0, 0, 0, 360, "TheHuntIsOn")
    Loop
	
  MeleeProper:
  MissileProper:
    ASOL BA 8 A_FaceTarget
    ASOL F 2 A_SkullAttack (35)
    ASOL E 2
    TNT1 A 0 A_JumpIfInventory("RLTheHungryGetHungrier", 0, "YOUDIDABADTHING")
    TNT1 A 0 A_GiveInventory("RLTheHungryGetHungrier", 2)
  MissileLoop:
    ASOL FABAFE 2
    Loop
	
  YOUDIDABADTHING: // if you were just watching it while it tried to kill you, enjoy your new swarm of the hungry
    TNT1 A 0 A_RadiusGive("RLTheHungrySpawnWave", 8192, RGF_MONSTERS, 1)
    Goto MissileLoop
	
  PainMain:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    ASOL I 3 
    ASOL I 3 A_Pain
    Goto See
	
  DeathMain:
    TNT1 A 0 A_GiveInventory ("RLArmageddonLostSoulKillIncrementer", 1, AAPTR_PLAYER1) // Used for tallying total kills and bestiary info
    TNT1 A 0 A_RadiusGive("RLTheHungryHungers", 512, RGF_MONSTERS, 1)
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
    ASOL I 5
    ASOL J 5 A_Scream
    ASOL K 5
    ASOL L 5 A_NoBlocking
    ASOL MN 5 A_SetFloorClip
    ASOL O 0
    "####" "#" 0 A_Jump(256, "DeathEnd")
    Goto DeathEnd
  DeathLoop:
    ASOL O 1 A_CheckSight("FadeAway")
    Loop
  FadeAway:
    ASOL O 1 A_FadeTo(0, 0.1, 1)
    Loop
  DeathFrame:
    ASOL O 0
    "####" "#" 0 A_Jump(256, "DeathEnd")
    Goto DeathEnd
	
  XDeathMain:
    TNT1 A 0 A_GiveInventory ("RLArmageddonLostSoulKillIncrementer", 1, AAPTR_PLAYER1) // Used for tallying total kills and bestiary info
    TNT1 A 0 A_RadiusGive("RLTheHungryHungers", 512, RGF_MONSTERS, 1)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryGibber", 0,0,0, VelX,VelY,VelZ, 0, SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION)
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    ASOL P 3 
    ASOL Q 3 A_XScream
    ASOL R 3 A_NoBlocking  
    ASOL STU 3
    Stop
  SimpleXDeathMain:
    TNT1 A 0 A_GiveInventory ("RLArmageddonLostSoulKillIncrementer", 1, AAPTR_PLAYER1) // Used for tallying total kills and bestiary info
	TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    ASOL P 3 
    ASOL Q 3 A_XScream
    ASOL R 3 A_NoBlocking  
    ASOL STU 3
    Stop
	
	//Ice
  GenericFreezeDeath:
	"####" "#" 0 A_GiveInventory ("RLArmageddonLostSoulKillIncrementer", 1, AAPTR_PLAYER1) // Used for tallying total kills and bestiary info
	TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    "####" "#" 0 A_Jump(256, "GenericFreezeMana")
    Goto GenericFreezeMana
//==============================================================================
//					--	DOOMRL ARSENAL STUFF  --
//==============================================================================
  XDeath.RigelBlast:
  Death.RigelBlast:
    TNT1 A 0 A_GiveInventory ("RLArmageddonLostSoulKillIncrementer", 1, AAPTR_PLAYER1) // Used for tallying total kills and bestiary info
	TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_PlaySound("weapons/dn2gunexplosion", CHAN_BODY)
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_SpawnItemEx ("RLDuke2MediumExplosion", 0,0,16, 0,0,0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 35
    Stop
	
//==============================================================================
//					--	Special Monster Attacks  --
//==============================================================================

//==============================================================================
//					--	Final Doomer Compatibility Stuff  --
//==============================================================================
  Death.Maser:
  XDeath.Maser:
    "####" "#" 0 A_FaceTarget
    "####" "#" 0 A_ChangeVelocity (frandom(-5.0,-15.0), 0, 0, CVF_RELATIVE)
    "####" "#" 0 A_SpawnItemEx ("RLReducedGibber", 0,0,0, VelX,VelY,VelZ, 0, SXF_ABSOLUTEMOMENTUM | SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
    "####" "#" 0 A_Jump(256, "XDeath")
	Goto XDeath
	
  DeathXSparkFrame:
    "####" "#" 0 A_JumpIfInTargetInventory("Mana",0,2)
    "####" "#" 0 A_DropItem("MinorManaPod",-1,54)
	"####" "#" 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
    "####" "#" 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    ASOL I 0
    "####" "#" 0 A_Jump(256, "DeathXSpark")
	Goto DeathXSpark
	
//==============================================================================
//					--	Guncaster Compatibility Stuff  --
//==============================================================================
	MidasFrame1:
		ASOL I 0 A_SpawnItemEx("Midas_Statue_LostSoul", 0,0,0, 0,0,0, 0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPRITEFRAME)
		TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
		TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		TNT1 A 0 A_JumpIf(1, "DestroyedEnd")
		Goto DestroyedEnd
	MidasFrame2:
		ASOL I 0 A_SpawnItemEx("Midas_Statue_LostSoul", 0,0,0, frandom(-10.0,-12.0),0,frandom(1.0,3.0), 0,SXF_TRANSFERPOINTERS|SXF_SETMASTER|SXF_TRANSFERSPRITEFRAME)
		TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
		TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		TNT1 A 10 A_KillChildren
		TNT1 A 0 A_JumpIf(1, "DestroyedEnd")
		Goto DestroyedEnd
	MidasFrame3:
		ASOL I 0 A_SpawnItemEx("Midas_Statue_LostSoul", 0,0,0, frandom(-15.0,-20.0),0,frandom(10.0,15.0), 0,SXF_TRANSFERPOINTERS|SXF_SETMASTER|SXF_TRANSFERSPRITEFRAME)
		TNT1 A 0 A_JumpIf(CallACS("DRLA_thehungryinfestation") == 0, 2)
		TNT1 A 0 A_SpawnItemEx ("RLTheHungryAreNeverTooFarAway", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		TNT1 A 10 A_KillChildren
		TNT1 A 0 A_JumpIf(1, "DestroyedEnd")
		Goto DestroyedEnd
		
	//Gumpop
	Death.Acid:
		ASOL I 1 A_ChangeFlag("SOLID",0)
		"####" "#" 1 A_Playsound("Gumpop/Expand",5)
		"####" "###################################" 1 A_SetScale(ScaleX+frandom(0.01,0.025))
		"####" "#" 1 A_Playsound("Gumpop/Pop",5)
		"####" "#" 0 A_Jump(256,"XDeathMain")
		Goto XDeathMain
		
	//Thunderstruck
	Pain.Lightning:
		"####" "#" 0 A_Jump(164,1)
		Goto Pain
		ASOL I 0 A_ChangeFlag("NOPAIN",1)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock + 10)
		"####" "#" 0 A_Jump(256,"LightningLock")
		Goto LightningLock
		
	//Cauterizer
	Pain.LightningBlue:
		"####" "#" 0 A_Jump(48,1)
		Goto Pain
		ASOL I 0 A_ChangeFlag("NOPAIN",1)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock + 10)
		"####" "#" 0 A_Jump(256,"BlueLightningLock")
		Goto BlueLightningLock
		
		// Overwrite these for monsters that do not leave corpses behind
	//Dreadful
	XDeath.Tiberium:
		"####" "#" 0 A_Jump(256, "XDeath")
		Goto XDeath
	XDeath.BlueTiberium:
		"####" "#" 0 A_Jump(256, "XDeath")
		Goto XDeath
	//Fire and Lightning
	XDeath.Fire:
		"####" "#" 0 A_Jump(256,"XDeath")
		Goto XDeath
  }
}

Actor RLTheHungryHungers : Inventory
{
inventory.maxamount 1
}

Actor RLTheHungryGetHungrier : Inventory
{
inventory.maxamount 80
}

ACTOR RLTheHungrySpawnWave : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0 
    TNT1 A 0 A_JumpIf(CheckClass("RLArmageddonLostSoul", AAPTR_DEFAULT, TRUE), "TimeForFun")
    Stop
  TimeForFun:
    TNT1 A 0 A_JumpIfHealthLower (1, "Nah")
    TNT1 A 0 A_CheckSight ("OhYeahLetsDoThis")
    Stop
  OhYeahLetsDoThis:
    TNT1 A 0 A_TakeInventory("RLTheHungryGetHungrier", 99999)
	TNT1 A 0 A_SpawnItemEx ("RLTheHungrySpawner", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
    Stop
  Nah:
    TNT1 A 0 
    Stop
  }
}


actor RLTheHungrySpawner
{
    Radius 14
    Height 30
	+ISMONSTER // so they don't spawn with NoMonsters on and eat mab's face
	+NOBLOCKMAP
	+NOTIMEFREEZE
    States
    {
    Spawn:
	    TNT1 A 0 NoDelay A_Jump (256, 1,2,3,4,5,6)
	    TNT1 AAAAAAAAAAA 0 A_SpawnItemEx ("RLTheHungrySingleSpawner", random(-1,1), random(-1,1), random(0,16))
		Stop
    }
}

actor RLTheHungrySingleSpawner
{
    Radius 20
    Height 30
    Speed 15
    Species "RLTheHungry"
	+SOLID
	+NOBLOCKMAP
	+NOTONAUTOMAP
	+THRUSPECIES
	+NOTIMEFREEZE
    States
    {
    Spawn:
	    TNT1 A 0
	    TNT1 AAAAAAA 0 A_Wander
	    TNT1 A 0 A_SpawnItemEx ("RLArmageddonLostSoul")
		Stop
    }
}

actor RLTheHungryAreNeverTooFarAway : RLTheHungrySingleSpawner
{
	-SOLID
	+NOTRIGGER
	+BLOCKEDBYSOLIDACTORS
    States
    {
    Spawn:
	    TNT1 A 0
	    TNT1 AAAAAAAA 0 A_Wander
	    TNT1 A 1 A_Wander
	    TNT1 AAAAAAAA 0 A_Wander
	    TNT1 A 1 A_Wander
	    TNT1 AAAAAAAA 0 A_Wander
	    TNT1 A 1 A_Wander
	    TNT1 AAAAAAAA 0 A_Wander
	    TNT1 A 1 A_Wander
	    TNT1 A random(6100, 61500)
	    TNT1 A 0 A_Jump (128, "NoMore") // slowly dissipate their numbers, 50/50 chance of respawning
	    TNT1 A 0 A_CheckSight (1)
		Loop
	    TNT1 A 0 A_SpawnItemEx ("RLArmageddonLostSoul")
        TNT1 A 0 A_RadiusGive("RLTheHungryHungers", 512, RGF_MONSTERS, 1)
		Stop
    NoMore:
	    TNT1 A 0
		Stop
    }
}

ACTOR RLCCArmageddonLostSoul : RLArmageddonLostSoul
{
  +TOUCHY
  States
  {
  See:
    ASOL AAAABBBB random(1,3)
    Loop
  Melee:
  Missile:
    ASOL BAFEFA 2
    Loop
  Death:
    ASOL I 5
    ASOL J 5 A_Scream
    ASOL K 5
    ASOL L 5 A_NoBlocking
    ASOL MN 5 A_SetFloorClip
    ASOL O -1
	Stop
  }
}
